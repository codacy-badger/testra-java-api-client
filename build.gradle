buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('io.swagger:swagger-codegen:2.4.1')
        classpath('org.projectlombok:lombok:1.18.4')
    }
}

plugins {
    id 'idea'
    id 'project-report'
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.8.1"
}

repositories {
    mavenLocal()
    jcenter()
}

ext {
    jackson_version = '2.4.2'
    jersey_version = '1.18'
    jodatime_version = '2.3'
    junit_version = '4.12'
    lombok_version = '1.18.4'
    logback_version = '1.2.3'
    slf4j_version = '1.7.25'
    okhttp_version = '2.7.5'
    gson_version = '2.8.5'
    gson_fire_version = '1.8.3'
    swagger_anno_version = '1.5.21'
}

group 'tech.testra'
version '0.18'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

def swaggerInput = "swagger.yaml"
def swaggerOutputDir = file('build/swagger')
task generateApi {
    inputs.file(swaggerInput)
    outputs.dir(swaggerOutputDir)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(swaggerInput)
        config.setOutputDir(swaggerOutputDir.path)
        config.setLang('java')
        config.setAdditionalProperties([
                'invokerPackage': 'tech.testra.java.client',
                'modelPackage'  : 'tech.testra.java.client.model',
                'apiPackage'    : 'tech.testra.java.client.api',
                'dateLibrary'   : 'java8'
        ])
        config.setImportMappings([
                'Dog': 'tech.testra.jvm.client.model.Dog'
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

clean.doFirst {
    delete(swaggerOutputDir)
}

configurations {
    swagger
}

sourceSets {
    swagger {
        compileClasspath = configurations.swaggerCompile
        java {
            srcDir file("$buildDir/swagger/src/main/java")
        }
    }
    main {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
    test {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
}

compileSwaggerJava.dependsOn generateApi
classes.dependsOn swaggerClasses
compileJava.dependsOn compileSwaggerJava

dependencies {
    swaggerCompile "io.swagger:swagger-annotations:$swagger_anno_version"
    swaggerCompile "com.squareup.okhttp:okhttp:$okhttp_version"
    swaggerCompile "com.squareup.okhttp:logging-interceptor:$okhttp_version"
    swaggerCompile "com.google.code.gson:gson:$gson_version"
    swaggerCompile "io.gsonfire:gson-fire:$gson_fire_version"

    compile sourceSets.swagger.output
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "ch.qos.logback:logback-core:$logback_version"
    testCompile "junit:junit:$junit_version"

    runtime "com.squareup.okhttp:okhttp:$okhttp_version"
    runtime "com.squareup.okhttp:logging-interceptor:$okhttp_version"
    runtime "com.google.code.gson:gson:$gson_version"

    compileOnly "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"
}

jar {
    from "$buildDir/classes/java/swagger"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom {
                url = 'https://github.com/testra-tech/testra-java-api-client'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'jhorsman'
                        name = 'Jonathan Horsman'
                    }
                    developer {
                        id = 'sudokar'
                        name = 'sudokar'
                    }
                }
                scm {
                    url = 'https://github.com/testra-tech/testra-java-api-client'
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getProperty('bintray.user')
            password = System.getProperty('bintray.key')
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(System.getProperty('build.number'))
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    override = true
    dryRun = false
    publish = true
    pkg {
        repo = 'testra'
        name = "$project.name"
        publicDownloadNumbers = true
        desc = 'Java api client for Testra'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/testra-tech/testra-java-api-client'
        version {
            name = "$project.version"
            desc = "$project.version"
            released = new Date()
        }
    }
}
