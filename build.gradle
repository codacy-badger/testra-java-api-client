buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('io.swagger.codegen.v3:swagger-codegen:3.0.4')
    }
}

plugins {
    id 'idea'
    id 'project-report'
    id 'java'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.jfrog.artifactory" version "4.8.1"
}

repositories {
    mavenLocal()
    jcenter()
}

group 'io.github.testra'
version '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

def swaggerInput = "swagger.yaml"
def swaggerOutputDir = file('build/swagger')
task generateApi {
    inputs.file(swaggerInput)
    outputs.dir(swaggerOutputDir)
    doLast {
        def config = new CodegenConfigurator()
        config.setInputSpec(swaggerInput)
        config.setOutputDir(swaggerOutputDir.path)
        config.setLang('java')
        config.setLibrary('retrofit2')
        config.setAdditionalProperties([
                'invokerPackage': 'io.github.testra.java.client',
                'modelPackage'  : 'io.github.testra.java.client.model',
                'apiPackage'    : 'io.github.testra.java.client.api',
                'dateLibrary'   : 'java8'
        ])
        new DefaultGenerator().opts(config.toClientOptInput()).generate()
    }
}

clean.doFirst {
    delete(swaggerOutputDir)
}

configurations {
    swagger
}

sourceSets {
    swagger {
        compileClasspath = configurations.swaggerCompile
        java {
            srcDir file("$buildDir/swagger/src/main/java")
        }
    }
    main {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
    test {
        compileClasspath += swagger.output
        runtimeClasspath += swagger.output
    }
}

compileSwaggerJava.dependsOn generateApi
classes.dependsOn swaggerClasses
compileJava.dependsOn compileSwaggerJava

ext {
    okhttp_version = '3.13.1'
    swagger_anno_version = '1.5.21'
    retrofit_version = '2.5.0'
    gson_version = '2.8.5'
    gson_fire_version = '1.8.3'
    apache_oltu_oauth2_version = '1.0.0'
    javax_anno_version = '1.3.2'
}

dependencies {
    swaggerCompile "io.swagger:swagger-annotations:$swagger_anno_version"
    swaggerCompile "com.squareup.okhttp3:okhttp:$okhttp_version"
    swaggerCompile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    swaggerCompile "com.squareup.retrofit2:retrofit:$retrofit_version"
    swaggerCompile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    swaggerCompile "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    swaggerCompile "org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:$apache_oltu_oauth2_version"
    swaggerCompile "com.google.code.gson:gson:$gson_version"
    swaggerCompile "io.gsonfire:gson-fire:$gson_fire_version"
    swaggerCompile "javax.annotation:javax.annotation-api:$javax_anno_version"

    compile sourceSets.swagger.output
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    runtime "com.squareup.okhttp3:okhttp:$okhttp_version"
    runtime "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    runtime "com.squareup.retrofit2:retrofit:$retrofit_version"
    runtime "com.squareup.retrofit2:converter-gson:$retrofit_version"
    runtime "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    runtime "org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:$apache_oltu_oauth2_version"
    runtime "com.google.code.gson:gson:$gson_version"
    runtime "io.gsonfire:gson-fire:$gson_fire_version"
}

jar {
    from "$buildDir/classes/java/swagger"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom {
                url = 'https://github.com/testra/testra-java-api-client'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'sudokar'
                        name = 'sudokar'
                    }
                    developer {
                        id = 'jhorsman'
                        name = 'Jonathan Horsman'
                    }
                }
                scm {
                    url = 'https://github.com/testra/testra-java-api-client'
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getProperty('bintray.user')
            password = System.getProperty('bintray.key')
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(System.getProperty('build.number'))
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    override = true
    dryRun = false
    publish = true
    pkg {
        repo = 'java-deps'
        name = "$project.name"
        publicDownloadNumbers = true
        desc = 'Java api client for Testra'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/testra/testra-java-api-client'
        version {
            name = "$project.version"
            desc = "$project.version"
            released = new Date()
        }
    }
}
